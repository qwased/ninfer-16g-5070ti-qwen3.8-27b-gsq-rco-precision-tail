#pragma once

// ninfer::ops - split-KV causal small-T attention shared scaffolding. The BF16 and
// int8 partial kernels live in causal_attention_small_t_bf16.cuh and
// causal_attention_small_t_i8.cuh respectively; they are fully separate kernels (no
// shared body) so each KV format can be optimized independently. This header owns
// only what both share: layout constants, device helpers, and the split reducer.

#include "ops/common/math.cuh"
#include "ops/common/mma.cuh"
#include "ops/common/warp.cuh"
#include "ops/softmax_attention/dense/causal_cache/geometry.cuh"
#include "ops/kernel/paged_kv_address.cuh"

#include <cuda_bf16.h>
#include <math_constants.h>

#include <cstdint>

namespace ninfer::ops {

inline constexpr int kCausalHeadDim = 256;

// The most keys one split may cover: a split stages at most 64 physical-page IDs, and two 64-key
// pages go to key-tile rounding and page alignment. A split that must cover more, past the maximum
// split count, reads its pages from the block table instead.
inline constexpr int kCausalSmallTSplitKeyLimit = 3968;

struct CausalAppendInput {
    static constexpr bool writes_cache = true;
    const __nv_bfloat16* k;
    const __nv_bfloat16* v;
};

struct CausalCachedInput {
    static constexpr bool writes_cache = false;
};

template <typename Geometry>
__device__ __forceinline__ std::int64_t causal_cache_index(int physical_page, int kv_head, int d,
                                                           int page_offset) {
    return paged_kv_element_offset<kCausalHeadDim, Geometry::KVHeads>(physical_page, kv_head,
                                                                      page_offset, d);
}

template <typename Geometry>
__device__ __forceinline__ std::int64_t causal_q_index(int q_head, int d, int token = 0) {
    return static_cast<std::int64_t>(d) + static_cast<std::int64_t>(kCausalHeadDim) *
                                              (static_cast<std::int64_t>(q_head) +
                                               static_cast<std::int64_t>(Geometry::QHeads) * token);
}

template <typename Geometry>
__device__ __forceinline__ std::int64_t kv_cache_int8_new_index(int kv_head, int d, int token = 0) {
    return static_cast<std::int64_t>(d) +
           static_cast<std::int64_t>(kCausalHeadDim) *
               (static_cast<std::int64_t>(kv_head) +
                static_cast<std::int64_t>(Geometry::KVHeads) * token);
}

template <typename Geometry>
__device__ __forceinline__ std::int64_t causal_partial_acc_index(int q_head, int d, int token,
                                                                 int split, int tokens) {
    return static_cast<std::int64_t>(d) +
           static_cast<std::int64_t>(kCausalHeadDim) *
               (static_cast<std::int64_t>(q_head) +
                static_cast<std::int64_t>(Geometry::QHeads) *
                    (static_cast<std::int64_t>(token) + static_cast<std::int64_t>(tokens) * split));
}

template <typename Geometry>
__device__ __forceinline__ std::int64_t causal_partial_stat_index(int q_head, int token, int split,
                                                                  int tokens) {
    return static_cast<std::int64_t>(q_head) +
           static_cast<std::int64_t>(Geometry::QHeads) *
               (static_cast<std::int64_t>(token) + static_cast<std::int64_t>(tokens) * split);
}

template <typename Geometry>
__device__ __forceinline__ bool causal_valid_q_head(int kv_head, int q_head) {
    return kv_head >= 0 && kv_head < Geometry::KVHeads && q_head >= kv_head * Geometry::GroupSize &&
           q_head < (kv_head + 1) * Geometry::GroupSize && q_head < Geometry::QHeads;
}

template <typename Geometry>
__device__ __forceinline__ int causal_small_t_default_splits(int window) {
    int target_keys_per_split = 480 / Geometry::SmallTSplitScale;
    if (window <= 4096) {
        target_keys_per_split = 64 / Geometry::SmallTSplitScale;
    } else if (window <= 8198) {
        target_keys_per_split = 128 / Geometry::SmallTSplitScale;
    } else if (window <= 16390) {
        target_keys_per_split = 256 / Geometry::SmallTSplitScale;
    }
    constexpr int kMinSplits = 4 * Geometry::SmallTSplitScale;
    int splits               = div_up(window, target_keys_per_split);
    splits                   = splits > kMinSplits ? splits : kMinSplits;
    return splits < Geometry::SmallTMaximumSplits ? splits : Geometry::SmallTMaximumSplits;
}

// wave_splits, when positive, is the launch's split count per full wave of CTAs: a count above it
// drops to the fewest whole waves that still keep every split within kCausalSmallTSplitKeyLimit.
template <typename Geometry, bool Int8>
__device__ __forceinline__ int causal_small_t_active_splits(int window, int launch_capacity,
                                                            int tokens, int wave_splits = 0) {
    if (window <= 0) { return launch_capacity; }
    int splits = 0;
    if constexpr (Int8) {
        if (tokens == 5 && window > 128 && window <= 512) {
            splits = div_up(window, 32 / Geometry::SmallTSplitScale);
        } else if (tokens >= 6 && window > 128 && window <= 160) {
            constexpr int kKeysPerSplit = Geometry::SmallTSplitScale == 2 ? 17 : 24;
            splits                      = div_up(window, kKeysPerSplit);
        } else if (tokens >= 6 && window > 5000 && window <= 8198) {
            splits             = div_up(window, 192 / Geometry::SmallTSplitScale);
            constexpr int kMin = 4 * Geometry::SmallTSplitScale;
            constexpr int kMax = 42 * Geometry::SmallTSplitScale;
            splits             = splits > kMin ? splits : kMin;
            splits             = splits < kMax ? splits : kMax;
        } else {
            splits = causal_small_t_default_splits<Geometry>(window);
        }
    } else {
        splits = causal_small_t_default_splits<Geometry>(window);
    }
    if (wave_splits > 0 && splits > wave_splits) {
        const int whole_waves =
            wave_splits * div_up(div_up(window, kCausalSmallTSplitKeyLimit), wave_splits);
        splits = splits < whole_waves ? splits : whole_waves;
    }
    return splits < launch_capacity ? splits : launch_capacity;
}

// Exact-tail split partition (WP3). `tail_tokens` is the configured retention N: zero (or a
// negative, or an absent tail) leaves the partition the identity -- body_window == window,
// body_active == total_active, tail_active == 0 -- so a launch without a tail is unchanged bit for
// bit. Beyond that, the newest min(N, window) keys move from the body to the tail, and the splits
// the body no longer uses become the tail's, at the same global indices the reducer merges.
struct CausalSmallTTailPartition {
    int body_window; // the quantized body covers keys [0, body_window)
    int body_active; // splits [0, body_active) belong to the body partial
    int tail_active; // splits [body_active, body_active + tail_active) belong to the tail partial
};

template <typename Geometry, bool Int8>
__device__ __forceinline__ CausalSmallTTailPartition
causal_small_t_tail_partition(int window, int tail_tokens, int launch_capacity, int tokens,
                              int wave_splits = 0) {
    const int total_active =
        causal_small_t_active_splits<Geometry, Int8>(window, launch_capacity, tokens, wave_splits);
    const int tail_keys   = tail_tokens > 0 ? (tail_tokens < window ? tail_tokens : window) : 0;
    const int body_window = window - tail_keys;
    // The reducer merges exactly the splits in [0, total_active) and the body and the tail between
    // them have to cover the whole window: the body owns keys [0, body_window) and the tail owns
    // [body_window, window). Holding the body inside the reducer's range is not enough -- the tier
    // floor (kMinSplits) makes active_splits(body_window) equal active_splits(window) for nearly
    // every short window, so a body clamped only at total_active takes every split and the newest
    // tail_keys keys fall out of the softmax entirely (they are in neither partial, so neither the
    // m/l merge nor the weighted accumulator ever sees them). Whenever the tail has keys the body
    // therefore stops one split short of the range, and a non-empty body keeps at least one split
    // so the keys below body_window stay covered as well -- the body's own tier can ask for more
    // splits than the whole window's, because the INT8 token-count tiers are not monotonic in the
    // window, and an empty body asks for none.
    int body_active = 0;
    if (body_window > 0) {
        body_active = causal_small_t_active_splits<Geometry, Int8>(body_window, launch_capacity,
                                                                   tokens, wave_splits);
        const int body_limit = tail_keys > 0 ? total_active - 1 : total_active;
        if (body_active > body_limit) { body_active = body_limit; }
        if (body_active < 1) { body_active = 1; }
    }
    return CausalSmallTTailPartition{body_window, body_active, total_active - body_active};
}

// Quantized storages take the plain default tier. This used to ask for SmallTMaximumSplits at
// tokens==1 and window>8198 while the host granted that capacity to fp8 only, so nvfp4 and k8v4
// silently ran fewer splits than this function returned and the two sides disagreed about intent.
// Measurement said the host's default was the better number for all of them (see
// causal_small_t_split_count in small_t.cu), so the request is gone rather than the grant
// extended, and this now agrees with the host by construction.
template <typename Geometry>
__device__ __forceinline__ int
causal_small_t_quantized_active_splits(int window, int launch_capacity, int tokens) {
    (void)tokens;
    const int splits = causal_small_t_default_splits<Geometry>(window);
    return splits < launch_capacity ? splits : launch_capacity;
}

__device__ __forceinline__ int causal_small_t_tc_swz(int row, int col) {
    return (((col >> 3) ^ (row & 7)) << 3) | (col & 7);
}

template <typename Byte>
__device__ __forceinline__ void causal_small_t_store_byte_swizzled(Byte* tile, int row, int d,
                                                                   int d_b16_stride, Byte code) {
    const int col_b16 = d >> 1;
    const int byte    = d & 1;
    const int off     = (row * d_b16_stride + causal_small_t_tc_swz(row, col_b16)) * 2 + byte;
    tile[off]         = code;
}

__device__ __forceinline__ int causal_small_t_tc_swz32(int row, int col) {
    return (((col >> 3) ^ (row & 3)) << 3) | (col & 7);
}

// Signed int8 QK MMA, k=32 contraction. A = 16x32 s8 (4 regs/thread, 4 s8 each),
// B = 8x32 s8 col-major (2 regs/thread), D = 16x8 s32 (4 regs/thread). The A/B
// register byte layout is identical to the m16n8k16 bf16 fragments loaded by
// ldmatrix_x4/x2 over a d-contiguous int8 tile reinterpreted as
// b16 (two packed int8 per 16-bit lane), so the same ldmatrix helpers and XOR
// swizzle feed this MMA. The s32 accumulator layout matches the bf16 f32
// accumulator (c0/c1 -> row groupID, c2/c3 -> row groupID+8), so score
// consumption is unchanged; only per-64-group scale rescale differs.
template <typename Geometry>
__device__ __forceinline__ void causal_small_t_tc_row_to_qt(int row, int tokens, int kv_head,
                                                            int& q_head, int& token) {
    token             = row / Geometry::GroupSize;
    const int local_q = row - token * Geometry::GroupSize;
    q_head            = kv_head * Geometry::GroupSize + local_q;
}

// Merge one query/head's split statistics once per CTA. Published scalars are separate
// from the reduction/weight storage, so later writes cannot race another warp's scalar read.
template <class Geometry>
__device__ __forceinline__ float
causal_merge_split_statistics(const float* partial_m, const float* partial_l, int q_head, int token,
                              int tokens, int splits, float* weights, float* warp_sums,
                              float* scalars) {
    static_assert(Geometry::SmallTMaximumSplits <= 256);
    const int tid = threadIdx.x, lane = tid & 31, warp = tid >> 5;
    const auto index   = causal_partial_stat_index<Geometry>(q_head, token, tid, tokens);
    const float m      = tid < splits ? partial_m[index] : -CUDART_INF_F;
    const float warp_m = warp_max(m);
    if (lane == 0) warp_sums[warp] = warp_m;
    __syncthreads();
    if (warp == 0) {
        const float maximum = warp_max(tid < 8 ? warp_sums[tid] : -CUDART_INF_F);
        if (tid == 0) scalars[0] = maximum;
    }
    __syncthreads();
    const float maximum     = scalars[0];
    const float l           = tid < splits ? partial_l[index] : 0.0f;
    const float weight      = l > 0.0f && maximum > -CUDART_INF_F ? expf(m - maximum) : 0.0f;
    const float denominator = block_reduce_sum<256>(l * weight, warp_sums);
    if (tid == 0) scalars[1] = denominator;
    __syncthreads();
    const float total = scalars[1];
    if (tid < splits) weights[tid] = total > 0.0f ? weight : 0.0f;
    __syncthreads();
    return total;
}

template <typename Geometry, int DChunk, bool Int8, bool MultiBatch, bool Masked, bool Offset>
__launch_bounds__(256) __global__ void causal_attention_small_t_reduce_output_kernel(
    const float* partial_acc, const float* partial_m, const float* partial_l,
    const std::int32_t* positions, const std::int32_t* valid_columns, std::int32_t tokens,
    std::int32_t full_width, std::int32_t column_begin, std::int32_t batch_size,
    std::int32_t split_count, std::int32_t wave_splits, __nv_bfloat16* out,
    const __nv_bfloat16* __restrict__ gate) {
    static_assert(DChunk > 0 && DChunk <= kCausalHeadDim);

    const int q_head      = static_cast<int>(blockIdx.x);
    const int d_start     = static_cast<int>(blockIdx.y) * DChunk;
    const int flat_column = static_cast<int>(blockIdx.z);
    int batch             = 0;
    int token             = flat_column;
    if constexpr (MultiBatch) {
        batch = flat_column / tokens;
        token = flat_column - batch * tokens;
    }
    const int tid = threadIdx.x;
    if (q_head >= Geometry::QHeads || token >= tokens) { return; }
    if constexpr (MultiBatch) {
        if (batch >= batch_size) { return; }
    }

    if constexpr (Offset) { positions += column_begin; }
    if constexpr (MultiBatch) { positions += batch * full_width; }
    const int last_pos = positions[tokens - 1];
    int output_column  = token;
    if constexpr (Offset) { output_column += column_begin; }
    if constexpr (MultiBatch) { output_column += batch * full_width; }
    if constexpr (Masked) {
        const int absolute_column = token + (Offset ? column_begin : 0);
        if (absolute_column >= valid_columns[batch]) {
            if (tid < DChunk && d_start + tid < kCausalHeadDim) {
                const auto index = causal_q_index<Geometry>(q_head, d_start + tid, output_column);
                // 0 * sigmoid(g) is 0 for every finite g, but not for a NaN gate, and the
                // standalone multiply would propagate that NaN. Do the multiply either way.
                out[index] =
                    gate == nullptr
                        ? __float2bfloat16(0.0f)
                        : __float2bfloat16_rn(0.0f * sigmoid(__bfloat162float(gate[index])));
            }
            return;
        }
    }


    if constexpr (MultiBatch) {
        const std::int64_t partial_acc_row = static_cast<std::int64_t>(batch) * kCausalHeadDim *
                                             Geometry::QHeads * tokens * split_count;
        const std::int64_t partial_stat_row =
            static_cast<std::int64_t>(batch) * Geometry::QHeads * tokens * split_count;
        partial_acc += partial_acc_row;
        partial_m += partial_stat_row;
        partial_l += partial_stat_row;
    }

    const int window = last_pos + 1;
    const int active_split_count =
        causal_small_t_active_splits<Geometry, Int8>(window, split_count, tokens, wave_splits);
    if (q_head == 0 && token == 0 && tid < 10 && tid < active_split_count) {
        const int i = causal_partial_stat_index<Geometry>(q_head, token, tid, tokens);
        printf("DBGRED win=%d asc=%d sc=%d tok=%d ws=%d split=%d m=%.6f l=%.6f\n", window,
               active_split_count, split_count, tokens, wave_splits, tid, partial_m[i], partial_l[i]);
    }

    __shared__ float weights[256], warp_sums[8], scalars[2];
    const float head_l =
        causal_merge_split_statistics<Geometry>(partial_m, partial_l, q_head, token, tokens,
                                                active_split_count, weights, warp_sums, scalars);
    const int d = d_start + tid;
    if (tid >= DChunk || d >= kCausalHeadDim) return;
    float numerator = 0.0f;
    for (int split = 0; split < active_split_count; ++split) {
        if (weights[split] != 0.0f)
            numerator +=
                partial_acc[causal_partial_acc_index<Geometry>(q_head, d, token, split, tokens)] *
                weights[split];
    }

    const float value    = (head_l > 0.0f) ? numerator / head_l : 0.0f;
    const auto out_index = causal_q_index<Geometry>(q_head, d, output_column);
    if (gate == nullptr) {
        out[out_index] = __float2bfloat16(value);
        return;
    }
    // Bit-exact with the standalone multiply: that kernel reads what attention stored, so it sees
    // the reduce result already rounded to BF16. Round here first, widen back, multiply in FP32,
    // round to nearest. Keeping the FP32 value would be more accurate and would move tokens.
    const __nv_bfloat16 reduced = __float2bfloat16(value);
    out[out_index] =
        __float2bfloat16_rn(__bfloat162float(reduced) * sigmoid(__bfloat162float(gate[out_index])));
}

} // namespace ninfer::ops
